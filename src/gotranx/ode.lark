?start: ode

?ode: (parameters | states | expressions | comment | NEWLINE) *
?parameter: NAME "=" expression  -> param
    | NAME "=" "ScalarParam" "(" expression ["," "unit" "=" UNIT_STR] ["," "description" "=" DESCRIPTION] ")" -> scalarparam

?assignment : VARIABLE "=" expression [ comment ] [ NEWLINE ]

comment : ("#" /.+/)+
parameters : "parameters" "("  (COMPONENT_NAME ",")* parameter ("," parameter)* [ NEWLINE ] ")"
states : "states" "("  (COMPONENT_NAME ",")* parameter ("," parameter)* [ NEWLINE ] ")"
expressions: (assignment)+
    | "expressions" "(" COMPONENT_NAME ("," COMPONENT_NAME)* ")" (assignment)+
    | "component" "(" COMPONENT_NAME ("," COMPONENT_NAME)* ")" (assignment)+


?expression: term (_add_op term)*
?term: factor (_mul_op factor)*
?factor: _unary_op factor | power
!_unary_op: "+"|"-"|"~"
!_add_op: "+"|"-"
!_mul_op: "*"|"/"
?power: signedatom ("**" factor)?


?signedatom: SIGN signedatom
    | func
    | logicalfunc
    | atom

?atom: scientific
    | variable
    | constant
    | "(" expression ")"

scientific: SCIENTIFIC_NUMBER
constant: PI
variable: VARIABLE
?func: funcname "(" expression ("," expression)* (",")* ")"
?funcname: COS
    | TAN
    | SIN
    | ACOS
    | ATAN
    | ASIN
    | LOG
    | LN
    | SQRT
    | EXP
    | ABS
    | ABSL
    | FLOOR
    | MOD

?logicalfunc: logicalfuncname "(" expression ("," expression)* (",")?  ")"
?logicalfuncname: CONTINUOUSCONDITIONAL
    | CONDITIONAL
    | LT
    | GT
    | LE
    | GE
    | AND
    | OR
    | EQ
    | NOT


CONTINUOUSCONDITIONAL: "ContinuousConditional"
CONDITIONAL: "Conditional"
EXP: "exp"
COS: "cos"
SIN: "sin"
TAN: "tan"
ACOS: "acos"
ASIN: "asin"
ATAN: "atan"
ABS: "Abs"
ABSL: "abs"
FLOOR: "floor"
LN: "ln"
LOG: "log"
SQRT: "sqrt"
GT: "Gt"
LT: "Lt"
GE: "Ge"
LE: "Le"
AND: "And"
OR: "Or"
EQ: "Eq"
NOT: "Not"
MOD: "Mod"


PI: "pi"
EULER: E2
VARIABLE: VALID_ID_START VALID_ID_CHAR*
VALID_ID_START: "a".."z"|"A".."Z"|"_"
VALID_ID_CHAR: VALID_ID_START | ("0".."9")
SCIENTIFIC_NUMBER: NUMBER ((E1 | E2) SIGN? NUMBER)?
E1: "E"
E2: "e"
SIGN: "+" | "-"
NEG: "-"


COMPONENT_NAME: ESCAPED_STRING
INFO: ESCAPED_STRING
DESCRIPTION: ESCAPED_STRING
UNIT_STR: ESCAPED_STRING


%ignore WS
%ignore WS_INLINE

%import common.CNAME -> NAME
%import common.NUMBER
%import common.WS
%import common.NEWLINE
%import common.WS_INLINE
%import common.ESCAPED_STRING
